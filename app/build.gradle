plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs.kotlin'
}

apply from: file("$rootDir/libraries.gradle")
apply from: file("$rootDir/ktlint.gradle")

android {
    namespace 'com.codestracture'
    testNamespace "com.codestracture.test"
    compileSdk 34

    defaultConfig {
        applicationId "com.codestracture"
        minSdk 23
        targetSdk 34
        versionCode 1
        versionName "1.0"

        buildConfigField 'String', 'BASE_URL', "\"https://portal.courtesycarsredhill.co.uk/CCRAPI/api/\""

        testApplicationId "com.codestracture.test"
        testInstrumentationRunner 'com.codestracture.test.Instrumentation'
        testInstrumentationRunnerArguments = [
         cucumberUseAndroidJUnitRunner: 'com.codestracture.test.Instrumentation',
         clearPackageData: 'true'
        ]
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }


    buildTypes {
        release {
            signingConfig signingConfigs.debug
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        viewBinding = true
        dataBinding = true
    }


    sourceSets {
        androidTest {
            assets.srcDirs = ['src/androidTest/assets']
        }
    }

    testOptions {
        execution = 'ANDROIDX_TEST_ORCHESTRATOR'
    }
    lint {
        abortOnError false
        absolutePaths true
        checkOnly 'NewApi', 'InlinedApi'
        checkAllWarnings true
        checkReleaseBuilds true
        enable 'RtlHardcoded', 'RtlCompat', 'RtlEnabled'
        ignoreWarnings false
        noLines true
        quiet true
        warningsAsErrors true
    }
}

dependencies {
    implementation google.coreKtx
    implementation google.appCompat
    implementation google.googleMaterial
    implementation google.constraintlayout
    implementation google.fragmentKtx
    implementation google.gson
    implementation google.navigation_fragment_ktx
    implementation google.navigation_ui_ktx

    //Epoxy
    implementation epoxy.epoxyRecyclerView
    kapt epoxy.epoxyRecyclerViewProcessor

    //EncryptedSharedPreferences
    implementation google.encryptedPreference

    implementation google.lifecycle_extensions_viewmodel
    implementation google.lifecycle_livedata
    implementation google.lifecycle_process

    //Reactive
    implementation reactive.java
    implementation reactive.android

    //DI
    implementation google.hiltAndroid
    kapt google.hiltAndroidCompiler
    implementation google.hiltWork

    //Retrofit & OkHttp
    implementation square.retrofit
    implementation square.retrofitRxAdapter
    implementation square.okhttpLogging
    implementation square.moshi
    implementation square.moshiConverter
    kapt square.moshiCodeGen

    implementation intuit.sdp
    implementation intuit.ssp

    implementation google.locationService

    implementation 'androidx.work:work-runtime-ktx:2.8.1'

    implementation 'com.google.androidbrowserhelper:androidbrowserhelper:2.4.0'

    implementation 'at.aau.itec.android.mediaplayer:mediaplayer:1.3.3'
    implementation 'at.aau.itec.android.mediaplayer:mediaplayer-dash:1.3.3'
    implementation 'at.aau.itec.android.mediaplayer:mediaplayer-gles:1.3.3'

    def withoutMTP = {
        exclude group: 'org.mobicents.protocols.ss7.mtp', module: 'mtp-api'
        exclude group: 'org.mobicents.protocols.ss7.mtp', module: 'mtp'
        exclude group: 'org.mobicents.protocols.ss7.sccp', module: 'sccp-api'
        exclude group: 'org.mobicents.protocols.ss7.sccp', module: 'sccp'
    }
    implementation 'org.mobicents.protocols.ss7.map:map-impl:8.0.112', withoutMTP

    androidTestImplementation "com.google.dagger:hilt-android-testing:2.48"
    kapt 'com.google.dagger:hilt-android-compiler:2.48'

    androidTestImplementation('androidx.test.espresso:espresso-core:3.5.1')
    androidTestImplementation('androidx.test:rules:1.4.0')
    androidTestImplementation('androidx.test:core:1.4.0')
    androidTestImplementation('androidx.test:runner:1.4.0')
    androidTestImplementation('androidx.test.ext:junit-ktx:1.1.5')

    androidTestImplementation "io.cucumber:cucumber-picocontainer:4.8.1"
    androidTestImplementation "io.cucumber:cucumber-android:4.9.0"
    androidTestImplementation "io.cucumber:cucumber-android-hilt:4.10.0"

    androidTestUtil 'androidx.test:orchestrator:1.4.0'
}

kapt {
    correctErrorTypes true
}